# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

with_smp = false
with_kernel_vm = true

# TODO(smklein): Move default args to a separate file
if (target_cpu == "x64") {
  with_kernel_vm = true
  kernel_defines = [
    "MEMBASE=0", # TODO(smklein): FIX ALL DEFINES BELOW HERE
    "MEMSIZE=0",
    "KERNEL_ASPACE_BASE=0xffff000000000000",
    "KERNEL_BASE=0xffffffff80000000",
    "KERNEL_LOAD_OFFSET=0x00200000",
  ]
} else if (target_cpu == "arm64") {
  with_kernel_vm = true
  kernel_defines = [
    "MEMBASE=0", # TODO(phosek): FIX ALL DEFINES BELOW HERE
    "MEMSIZE=0",
    "KERNEL_ASPACE_BASE=0xffff000000000000",
    "KERNEL_ASPACE_SIZE=0x0001000000000000",
    "USER_ASPACE_BASE=0x0000000001000000",
    "USER_ASPACE_SIZE=0x0000fffffe000000",
    "KERNEL_BASE=0xffff000000000000",
    "KERNEL_LOAD_OFFSET=0",
  ]
}

# All binary targets will get this list of configs by default.
_shared_binary_target_configs = [ "//build:compiler_defaults" ]

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = _shared_binary_target_configs

  # Executables get this additional configuration.
  configs += [ "//build:executable_ldconfig" ]
}
set_defaults("static_library") {
  configs = _shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = _shared_binary_target_configs
}
set_defaults("source_set") {
  configs = _shared_binary_target_configs
}

if (target_cpu == "x64") {
  host_toolchain = "//build/toolchain:x64"
} else if (target_cpu == "arm64") {
  host_toolchain = "//build/toolchain:arm64"
}
set_default_toolchain(host_toolchain)
