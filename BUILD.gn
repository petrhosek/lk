# This target will be built if no target is specified when invoking ninja.

import("//build/config.gni")

executable("kernel") {
  deps = [
    "//top",
  ]
  if (target_cpu == "x86" || target_cpu == "x64") {
    deps += [
      "//arch/x86",
      "//project:pc-x86-64-test",
      "//platform/pc",
    ]
  } else if (target_cpu == "arm64") {
    deps += [
      "//arch/arm64",
      "//platform/qemu-virt",
    ]
  }
}

group("kernel_binary") {
  deps = [
    ":objcopy_kernel",
  ]
}

action("objcopy_kernel") {
  script = "run_bash_command.py"

  inputs = [ "${root_build_dir}/kernel.elf" ]
  outputs = [ "${root_build_dir}/kernel.bin" ]

  args = [
    "${toolprefix}objcopy", "-O", "binary",
    rebase_path("${root_build_dir}/kernel.elf"),
    rebase_path("${root_build_dir}/kernel.bin"),
  ]

  deps = [ ":kernel" ]
}
