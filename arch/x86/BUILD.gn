assert(target_cpu == "x86" || target_cpu == "x64")

import("//build/linker_script.gni")

config("x86_config") {
  visibility = [ ":*" ]
  ldflags = [ "-dT", rebase_path("${root_gen_dir}/kernel.ld", root_out_dir)  ]
}

source_set("x86") {
  all_dependent_configs = [ ":x86_config" ]
  include_dirs = [
    "//lib/heap/include",
    "//lib/io/include",
    "//lib/libc/include",
  ]
  defines = kernel_defines
  sources = [
    "arch.c",
    "cache.c",
    "gdt.S",
    "thread.c",
    "faults.c",
    "descriptor.c",
    "fpu.c",
  ]
  if (target_cpu == "x64") {
    sources += [
      "64/start.S",
      "64/asm.S",
      "64/exceptions.S",
      "64/mmu.c",
      "64/ops.S",
    ]

  }
  deps = [
    ":kernel_ld",
  ]
}

linker_script("kernel_ld") {
  if (target_cpu == "x64") {
    infile = "32/kernel.ld"
  } else {
    infile = "64/kernel.ld"
  }
  outfile = "${root_gen_dir}/kernel.ld"
  defines = kernel_defines
}
