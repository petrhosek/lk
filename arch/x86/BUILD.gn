config("x86_config") {
  visibility = [ ":*" ]
  ldflags = [ "-dT", rebase_path("${root_gen_dir}/kernel.ld", root_out_dir)  ]
}

source_set("x86") {
  all_dependent_configs = [ ":x86_config" ]
  include_dirs = [
    "//lib/heap/include",
    "//lib/io/include",
    "//lib/libc/include",
  ]
  sources = [
    "arch.c",
    "cache.c",
    "gdt.S",
    "thread.c",
    "faults.c",
    "descriptor.c",
    "fpu.c",
  ]
  if (target_cpu == "x64") {
    sources += [
      "64/start.S",
      "64/asm.S",
      "64/exceptions.S",
      "64/mmu.c",
      "64/ops.S",
    ]

    deps = [
      ":generate_kernel_linker_script",
    ]
  }
}

# TODO(smklein): This shouldn't be x86-64 specific
action("generate_kernel_linker_script") {
  script = "generate_kernel_ld.py"
  sources = [ "64/kernel.ld" ]
  outputs = [ "${root_gen_dir}/kernel.ld" ]

  args = [ "--defines" ] + kernel_defines
  args += [
  "--infile", rebase_path("${root_out_dir}/../../arch/x86/64/kernel.ld"),
  "--outfile", rebase_path("${root_gen_dir}/kernel.ld")
  ]
}
